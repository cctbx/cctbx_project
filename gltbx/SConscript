import libtbx.load_env
Import("env_etc")

env_etc.gltbx_dist = libtbx.env.dist_path("gltbx")
env_etc.gltbx_include = libtbx.env.under_dist("gltbx", "..")
env_etc.gltbx_common_includes = [
  env_etc.libtbx_include,
  env_etc.gltbx_include,
  env_etc.scitbx_include,
  env_etc.boost_include,
]

if (not env_etc.no_boost_python):
  Import("env_no_includes_boost_python_ext")
  env = env_no_includes_boost_python_ext.Clone()
  env.Prepend(LIBS=["scitbx_boost_python"])
  if (env_etc.compiler == "win32_cl"):
    env.Append(LIBS=["glu32", "opengl32"])
  elif (env_etc.compiler.startswith("darwin_")):
    env.Append(SHLINKFLAGS=["-framework", "OpenGL"])
  else:
    libs = list(env["LIBS"])
    try: i = libs.index("m")
    except ValueError: i = len(libs)
    libs.insert(i, "GL")
    libs.insert(i, "GLU")
    env.Replace(LIBS=libs)
  env_etc.include_registry.append(
    env=env,
    paths=env_etc.gltbx_common_includes + [env_etc.python_include])
  #
  env_etc.gltbx_has_opengl_headers = False
  conf = env.Clone().Configure() # the .Clone() is to work around an unknown
                                 # problem under Windows 7 (64-bit) with
                                 # Python 2.7rc2
  if (conf.TryCompile("#include <gltbx/include_opengl.h>", extension=".cpp")):
    env_etc.gltbx_has_opengl_headers = True
  else:
    print "gltbx: OpenGL headers not available."
    print "gltbx: Compilation skipped."
  conf.Finish()
  #
  if (env_etc.gltbx_has_opengl_headers):
    for namespace,n_fragments_def,n_fragments_fun in [("gl", 8, 16),
                                                      ("glu", 2, 4)]:
      source=["%s_ext.cpp" % namespace]
      for i_fragment in xrange(n_fragments_def):
        source.append("#gltbx/%s_defines_%02d_bpl.cpp" % (
          namespace, i_fragment))
      for i_fragment in xrange(n_fragments_fun):
        source.append("#gltbx/%s_functions_%02d_bpl.cpp" % (
          namespace, i_fragment))
      env.SharedLibrary(
        target="#lib/gltbx_%s_ext" % namespace,
        source=source)
    env.SharedLibrary(
      target="#lib/gltbx_util_ext",
      source=["util_ext.cpp"])
    env.SharedLibrary(
      target="#lib/gltbx_viewer_utils_ext",
      source=["viewer_utils_ext.cpp"])
    env.SharedLibrary(
      target="#lib/gltbx_quadrics_ext",
      source=["quadrics_ext.cpp"])
    env.SharedLibrary(
      target="#lib/gltbx_fonts_ext",
      source=[
        "fonts_ext.cpp",
        "font_ucs_8x13.cpp",
        "font_ucs_9x15.cpp",
        "font_ucs_10x20.cpp"])
